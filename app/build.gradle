apply plugin: 'com.android.application'

android {
    compileSdkVersion = COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.7
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Android包
    implementation "com.android.support:support-v4:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:multidex:${MULTIDEX}"

    //Android屏幕适配
//    implementation "me.jessyan:autosize:${AUTOSIZE}"

    //文件下载管理器
    implementation "com.liulishuo.filedownloader:library:${FILEDOWNLOADER}"

    //EventBus
    implementation "org.greenrobot:eventbus:${EVENT_BUS}"

    //Gson
    implementation "com.google.code.gson:gson:${GSON}"

    //OkHttp+Retrofit+RxAndroid
    implementation "com.squareup.okhttp3:okhttp:${OKHTTP}"
    implementation "com.squareup.okhttp3:logging-interceptor:${OKHTTP}"
    implementation "com.github.ihsanbal:LoggingInterceptor:${LOGGING_INTERCEPTOR}"
    implementation "com.squareup.retrofit2:retrofit:${RETROFIT}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT}"
    implementation "com.squareup.retrofit2:converter-scalars:${RETROFIT}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT}"
    implementation "io.reactivex.rxjava2:rxjava:${RX_JAVA}"
    implementation "io.reactivex.rxjava2:rxandroid:${RX_ANDROID}"
    implementation "com.uber.autodispose:autodispose:${AUTO_DISPOSE}"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:${AUTO_DISPOSE}"

    //高德
    implementation "com.amap.api:navi-3dmap:${AMAP_NAVI_3DMAP}"

    //Glide
    implementation "com.github.bumptech.glide:glide:${GLIDE}"
    implementation "jp.wasabeef:glide-transformations:${GLIDE_TRANSFORMATIONS}"

    //SmartRefreshLayout
    implementation "com.scwang.smartrefresh:SmartRefreshLayout:${SMART_REFRESH}"
    implementation "com.scwang.smartrefresh:SmartRefreshHeader:${SMART_REFRESH}"

    //Badgeview
    implementation "q.rorbin:badgeview:${BADGE_VIEW}"

    //RecycleView适配器
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:${BRVAH}"

    //动态权限
    implementation 'pub.devrel:easypermissions:1.0.1'
}

configurations.all {
    exclude group: 'org.json'
    resolutionStrategy.eachDependency {details ->
        def requested = details.requested
        if (requested.group == 'com.android.support' && !requested.name.startsWith("multidex")) {
            details.useVersion "${SUPPORT_LIBRARY_VERSION}"
        }
        if (requested.group == 'com.squareup.okhttp3') {
            details.useVersion "${OKHTTP}"
        }
        if (requested.group == 'com.github.bumptech.glide') {
            details.useVersion "${GLIDE}"
        }
    }
}
